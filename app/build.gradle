apply plugin: "com.android.application"
apply plugin: 'com.antfortune.freeline'
def releaseTime() {
    return new Date().format("yyyyMMdd")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.onlyleo.gankgirl"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 15
        versionName "1.0.2"
        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    signingConfigs {
        release {
            keyAlias "only"
            keyPassword "136250"
            storeFile file("../gankgirl.jks")
            storePassword "136250"
        }
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            resValue "string", "app_name", "GankGirl_Debug"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            resValue "string", "app_name", "GankGirl"
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith(".apk")) {
                        def fileName = "gankgirl_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    productFlavors {
        tencent {}
        wandoujia {}
        google {}
        firim{}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:palette-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:gridlayout-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.jakewharton:butterknife:7.0.1"
    compile "com.squareup.retrofit2:converter-gson:2.0.0"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "com.orhanobut:logger:1.14"
    compile "com.squareup.okhttp3:mockwebserver:3.4.2"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.squareup.retrofit2:retrofit:x.y.z"
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"
    compile "com.daimajia.numberprogressbar:library:1.2@aar"
    compile "com.nineoldandroids:library:2.4.0"
    compile 'com.squareup.okio:okio:1.5.0'
    compile 'com.yanzhenjie:permission:1.0.3'
    compile 'com.github.gabrielemariotti.recyclerview:recyclerview-animators:0.3.0-SNAPSHOT@aar'
}
